-------------------------------------------------------------   GRAMATICA  ---------------------------------------------------------------
G = (N,T,P,S)
    Donde:
        N = { NO TERMINALES }
        T = { TERMINALES }
        P = { PRODUCCIONES }
        S = { inicio }
---------------------------NO TERMINALES--------------------------------------------------------------------------------------------------
NO TERMINALES = {inicio,expresion,tipodedato,declaracion, tipodegrafica, declaracionhistograma, declaracionbarras, declaracionpie, contenidopie, declaracionlinea,
tipodato, recorrido, instrucciones, arreglos, operaciones, operacion, elementos, valorpie, datoslabel, instrucciongrafica, buclegrafica,funcionesgrafica,
instruccionhistograma, contenidohistograma, instruccionbarras, contenidobarras, instruccionlinea, contenidolinea, valorexpresion, operacionesnest, arregloexpresiones,
printarreglos, printexpresiones, nt, ntexp, ciclo, datosgraficas,datosgraficas1}        
---------------------------TERMINALES-----------------------------------------------------------------------------------------------------
TERMINALES = {PARENTESIS_A, PARENTESIS_C, PUNTOYCOMA, COMA, SUM, RES, MUL, DIV, MOD, POR, MAS, ENTERO, DECIMAL, CADENA, PROGRAM, END, DOSPUNTOS,
VAR, MENORQUE, MAYORQUE, ARROBA, CORCHETE_ABRE, CORCHETE_CIERRA, R_DOUBLE, R_CHAR, ARR, MEDIA, MEDIANA, MODA, VARIANZA, MAX, MIN, PRINT, COLUMN,
EXEC, TITULO, VALUES, LABEL, EJEX, EJEY, GRAPHPIE, GRAPHBAR, GRAPHLINE, HISTOGRAM, CONSOLE, TITULOX, TITULOY, IDENTIFICADOR, GUION, IGUAL}        
---------------------------PRODUCCIONES---------------------------------------------------------------------------------------------------
<inicio> ::= PROGRAM <recorrido> END PROGRAM
;

<recorrido> ::= <instrucciones> 
        | <recorrido> <instrucciones>
;

<instrucciones> ::= <declaracion> 
        | <arreglos> 
        | <funcionesgrafica> 
        | <printarreglos>
        | <printexpresiones> 
        | error PUNTOYCOMA
;

<funcionesgrafica> ::= <declaracionhistograma> 
        | <declaracionbarras>
        | <declaracionpie>
        | <declaracionlinea>
;

<declaracion> ::= VAR DOSPUNTOS <tipodedato> DOSPUNTOS DOSPUNTOS IDENTIFICADOR MENOR GUION  <tipodato> END PUNTOYCOMA
        | VAR DOSPUNTOS <tipodedato> DOSPUNTOS DOSPUNTOS IDENTIFICADOR MENOR GUION <expresion> END PUNTOYCOMA
;

<tipodedato> ::= R_DOUBLE 
        |R_CHAR CORCHETE_ABRE CORCHETE_CIERRA
;
<tipodato> ::= DECIMAL 
        | CADENA 
        | ENTERO 
        | IDENTIFICADOR
;
<printarreglos> ::= CONSOLE DOSPUNTOS DOSPUNTOS COLUMN IGUAL IDENTIFICADOR  GUION MAYORQUE END PUNTOYCOMA
        | CONSOLE DOSPUNTOS DOSPUNTOS COLUMN IGUAL CADENA GUION MAYORQUE <nt> END PUNTOYCOMA
;        
<nt> ::= ARROBA IDENTIFICADOR
        |CORCHETE_ABRE <elementos> CORCHETE_CIERRA
;
<printexpresiones> ::= CONSOLE DOSPUNTOS DOSPUNTOS PRINT IGUAL <ciclo> END PUNTOYCOMA
;
<ntexp>::= IDENTIFICADOR
        | CADENA
        | ENTERO
        | DECIMAL
        | <expresion>       
;

<ciclo> ::= <ntexp> 
        | <ntexp> COMA <ciclo>
;

<declaracionlinea> ::= GRAPHLINE PARENTESIS_A <instruccionlinea> PARENTESIS_C END PUNTOYCOMA
;

<declaracionbarras> ::= GRAPHBAR PARENTESIS_A <instruccionbarras> PARENTESIS_C  END PUNTOYCOMA
;

<instruccionbarras> ::= <instruccionbarras> <contenidobarras> 
        | <contenidobarras>
;

<contenidobarras> ::= TITULOX DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA <datosgraficas> END PUNTOYCOMA
        | TITULOY DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA
        | TITULO DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas1> END PUNTOYCOMA 
        | EJEX DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas1> END PUNTOYCOMA
        | EJEY DOSPUNTOS DOSPUNTOS R_DOUBLE IGUAL <datosgraficas> END PUNTOYCOMA
        | EXEC GRAPHBAR END PUNTOYCOMA
;

<declaracionhistograma> ::= HISTOGRAM PARENTESIS_A <instruccionhistograma> PARENTESIS_C END PUNTOYCOMA
;

<instruccionhistograma> ::= <instruccionhistograma> <contenidohistograma> 
        | <contenidohistograma>
;

<contenidohistograma> ::= TITULO DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA 
        | VALUES DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas1> END PUNTOYCOMA
        | EXEC HISTOGRAM END PUNTOYCOMA
;


<declaracionlinea> ::= GRAPHLINE PARENTESIS_A PARENTESIS_C END PUNTOYCOMA
;

<instruccionlinea> ::= <instruccionlinea> <contenidolinea> 
        | <contenidolinea>
;

<contenidolinea> ::= TITULO DOSPUNTOS DOSPUNTOS CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA 
        | EJEX DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL  <datosgraficas1> END PUNTOYCOMA 
        | EJEY DOSPUNTOS DOSPUNTOS R_DOUBLE IGUAL <datosgraficas1> END PUNTOYCOMA 
        | TITULOX DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA
        | TITULOY DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA 
        | EXEC GRAPHLINE END PUNTOYCOMA
;

<declaracionpie> ::= GRAPHPIE PARENTESIS_A <instrucciongrafica> PARENTESIS_C END PUNTOYCOMA
;

<instrucciongrafica> ::= <instrucciongrafica> <contenidopie> 
        | <contenidopie>
;

<contenidopie> ::= TITULO DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA IGUAL <datosgraficas> END PUNTOYCOMA 
        | VALUES DOSPUNTOS DOSPUNTOS R_DOUBLE IGUAL <datosgraficas1> END PUNTOYCOMA
        | LABEL DOSPUNTOS DOSPUNTOS R_CHAR CORCHETE_ABRE CORCHETE_CIERRA igual  <datosgraficas1> END PUNTOYCOMA 
        | EXEC GRAPHPIE END PUNTOYCOMA
;

<arreglos> ::= ARR DOSPUNTOS <tipodedato> DOSPUNTOS DOSPUNTOS  ARROBA IDENTIFICADOR MENORQUE GUION CORCHETE_ABRE <elementos> CORCHETE_CIERRA END PUNTOYCOMA
;

<elementos> ::= <valorexpresion> 
        | <valorexpresion> COMA <elementos>
;
    
<expresion> ::= SUM PARENTESIS_A <valorexpresion> COMA <valorexpresion> PARENTESIS_C 
        | RES PARENTESIS_A <valorexpresion> COMA <valorexpresion> PARENTESIS_C
        | MUL PARENTESIS_A <valorexpresion> COMA <valorexpresion> PARENTESIS_C  
        | DIV PARENTESIS_A <valorexpresion> COMA <valorexpresion> PARENTESIS_C  
        | MOD PARENTESIS_A <valorexpresion> COMA <valorexpresion> PARENTESIS_C  
        | <operacionesnest>
;

<valorexpresion> ::= <expresion> 
        | DECIMAL 
        | IDENTIFICADOR 
        | ENTERO 
        | CADENA
;

<operacionesnest> ::= MEDIA PARENTESIS_A <arregloexpresiones> PARENTESIS_C
        | MEDIANA PARENTESIS_A <arregloexpresiones> PARENTESIS_C
        | MODA PARENTESIS_A <arregloexpresiones> PARENTESIS_C
        | VARIANZA PARENTESIS_A <arregloexpresiones> PARENTESIS_C 
        | MAX PARENTESIS_A <arregloexpresiones> PARENTESIS_C
        | MIN PARENTESIS_A <arregloexpresiones> PARENTESIS_C
;

<arregloexpresiones> ::= ARROBA IDENTIFICADOR 
        | CORCHETE_ABRE <elementos> CORCHETE_CIERRA
;

<datosgraficas> ::= ENTERO
        | DECIMAL
        | CADENA
        | ARROBA IDENTIFICADOR
        | IDENTIFICADOR
        | CORCHETE_ABRE <elementos> CORCHETE_CIERRA
 ;       
<datosgraficas1> ::=  ARROBA IDENTIFICADOR
        | CORCHETE_ABRE <elementos> CORCHETE_CIERRA
 ;       